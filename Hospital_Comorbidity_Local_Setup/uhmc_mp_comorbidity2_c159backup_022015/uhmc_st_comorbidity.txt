/*****************************************************************************
Source file name: uhmc_st_comorbidity.prg
Purpose: This is used for the "* Co-Morbidity" smart template.
Author: Neil Mendoza
Date: 12/29/2014
 
				GENERATED MODIFICATION CONTROL LOG
*****************************************************************************
Date       	Engineer          	Comments
---------- 	------------------ 	---------------------------------------------
 
****************************************************************************/
 
DROP PROGRAM   uhmc_st_comorbidity:dba go
CREATE PROGRAM  uhmc_st_comorbidity:dba
 
PROMPT "Output to File/Printer/MINE = " = MINE
WITH OUTDEV
 
declare cnt = i4
declare blobout = vc with noconstant("")
declare bsize = i4 with noconstant(32768),public
declare tmpcomorb = vc
declare tmpresult = vc
declare OCFCOMP_VALUE = f8 with constant(uar_get_code_by("MEANING",120,"OCFCOMP")),protect
DECLARE EDCOMORBIDITY_CD = f8 WITH protect,constant(uar_get_code_by("DISPLAYKEY",72,"EDCOMORBIDITY"))
 
SET RHEAD = CONCAT("{\rtf1\ansi \deff0{\fonttbl{\f0\fswiss MS Sans Serif;}",
					"}{\colortbl;\red0\green0\blue0;\red255\green255\blue255;}\deftab1134")
SET REOL = "\par "
SET WR = "\plain \f0 \fs18 \cb2 "
SET WB = "\plain \f0 \fs18 \b \cb2 "
SET UL = "\ul "
SET RTFEOF = "}"
 
declare ENCNTR_ID = f8
set ENCNTR_ID = REQUEST->VISIT[1].ENCNTR_ID
 
set REPLY->TEXT = RHEAD
 
select into "nl:"
from clinical_event ce
	,ce_blob cb
	,prsnl p
plan ce where ce.encntr_id = ENCNTR_ID
	and ce.event_cd = EDCOMORBIDITY_CD				;ED Co-Morbidity
	and ce.result_status_cd in (26.00,24.00,35.00)	;Final,Modified,Modified
	and ce.valid_from_dt_tm < cnvtdatetime(curdate,curtime3)
    and ce.valid_until_dt_tm > cnvtdatetime(curdate,curtime3)
join cb where cb.event_id = ce.event_id
	and cb.valid_from_dt_tm < cnvtdatetime(curdate,curtime3)
    and cb.valid_until_dt_tm > cnvtdatetime(curdate,curtime3)
join p where p.person_id = outerjoin(ce.performed_prsnl_id)
	and p.active_ind = outerjoin(1)
order by ce.event_id desc,cb.blob_seq_num
head ce.event_id
	tmpcomorb = concat("The following were committed on ",
					   trim(format(ce.performed_dt_tm,"@SHORTDATETIMENOSEC"),3),
					   " by ",
					   trim(p.name_full_formatted,3),":")
	REPLY->TEXT = concat(REPLY->TEXT,WB,UL,tmpcomorb,REOL)
detail
	if (cb.compression_cd = OCFCOMP_VALUE)
		blobout = notrim(fillstring(32768," "))
		call UAR_OCF_UNCOMPRESS(cb.blob_contents,textlen(cb.blob_contents),blobout,size(blobout),bsize)
		cnt = 1
		while (piece(blobout,char(10),cnt,"NOMORE") != "NOMORE")
			tmpcomorb = piece(trim(piece(blobout,char(10),cnt,"")),"[",1,trim(piece(blobout,char(10),cnt,"")))
			tmpresult = piece(trim(piece(blobout,char(10),cnt,"")),"[",2,"")
			if (size(trim(tmpresult)) > 0)
				tmpresult = concat("  [",tmpresult)
			else
				tmpresult = " "
			endif
			REPLY->TEXT = concat(REPLY->TEXT,WB,tmpcomorb,WR,tmpresult,REOL)
			;REPLY->TEXT = concat(REPLY->TEXT,WR,trim(piece(blobout,char(10),cnt,"")),REOL)
			cnt = cnt + 1
		endwhile
	else
		blobout = substring(1,cb.blob_length,cb.blob_contents)
		cnt = 1
		while (piece(blobout,char(10),cnt,"NOMORE") != "NOMORE")
			tmpcomorb = piece(trim(piece(blobout,char(10),cnt,"")),"[",1,trim(piece(blobout,char(10),cnt,"")))
			tmpresult = piece(trim(piece(blobout,char(10),cnt,"")),"[",2,"")
			if (size(trim(tmpresult)) > 0)
				tmpresult = concat("  [",tmpresult)
			else
				tmpresult = " "
			endif
			REPLY->TEXT = concat(REPLY->TEXT,WB,tmpcomorb,WR,tmpresult,REOL)
			;REPLY->TEXT = concat(REPLY->TEXT,WR,trim(piece(blobout,char(10),cnt,"")),REOL)
			cnt = cnt + 1
		endwhile
	endif
with nocounter
 
set REPLY->TEXT = concat(REPLY->TEXT,RTFEOF)
 
# EXIT_SCRIPT
 
END GO